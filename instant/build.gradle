plugins {
    id('com.android.application')
    id('org.jetbrains.kotlin.android')
    id('com.google.gms.google-services')
    id('com.google.firebase.crashlytics')
    id('com.google.secrets_gradle_plugin') version '0.6'
}

apply from: '../base/version.gradle'

android {
    compileSdk 31
    buildToolsVersion "31.0.0"

    defaultConfig {
        def instantVersionCode = project.properties["android.instantVersionCode"] as Integer

        applicationId "com.arnyminerz.escalaralcoiaicomtat"
        minSdk 26
        targetSdk 31
        versionCode instantVersionCode
        versionName VERSION_NAME + "-beta3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    // Configures the APK and AAB signing config
    signingConfigs {
        release {
            def props = new Properties()
            file("${rootDir}/local.properties").withInputStream { props.load(it) }

            storeFile file("../base/keystore.jks")
            storePassword props.getProperty("SIGNING_KEYSTORE_PASSWORD")
            keyAlias props.getProperty("SIGNING_KEY_ALIAS")
            keyPassword props.getProperty("SIGNING_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            setProperty("archivesBaseName", "EscalarAlcoiaIComtat-${android.defaultConfig.versionCode}-${android.defaultConfig.versionName}-instant")
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
}

android.applicationVariants.all { variant ->
    if (variant.buildType.name == "release") {
        variant.assemble.doLast {
            // This increases version code on release
            def versionPropsFile = file("../gradle.properties")
            Properties versionProps = new Properties()
            versionProps.load(new FileInputStream(versionPropsFile))
            def code = versionProps['android.instantVersionCode'].toInteger() + 1
            versionProps['android.instantVersionCode'] = code.toString()
            versionProps.store(versionPropsFile.newWriter(), null)
            logger.lifecycle("Increased instant version code to $code")
        }
    }
}

secrets {
    propertiesFileName 'local.properties'

    ignoreList.add("SIGNING_KEYSTORE_PASSWORD")
    ignoreList.add("SIGNING_KEY_ALIAS")
    ignoreList.add("SIGNING_KEY_PASSWORD")
}

dependencies {
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.activity:activity:1.3.0'
    implementation 'androidx.activity:activity-ktx:1.3.1'
    implementation 'androidx.activity:activity-compose:1.3.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.navigation:navigation-compose:2.4.0-alpha05"

    implementation 'com.google.android.material:material:1.4.0'
    implementation 'com.google.android.gms:play-services-instantapps:17.0.1'

    // Firebase Dependencies
    implementation platform('com.google.firebase:firebase-bom:28.2.0')
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-perf-ktx'
    implementation 'com.google.firebase:firebase-appindexing'
    implementation 'com.google.firebase:firebase-firestore-ktx'

    // Core Module
    implementation project(":core")

    // Live Data
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'

    // Coil image loading
    implementation 'io.coil-kt:coil:1.3.0'
    implementation 'io.coil-kt:coil-compose:1.3.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
}