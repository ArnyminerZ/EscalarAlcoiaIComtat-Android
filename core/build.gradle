plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'org.jetbrains.dokka'
    id 'com.google.devtools.ksp'
}

android {
    compileSdk 33
    buildToolsVersion "33.0.0"

    defaultConfig {
        minSdk 24
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        vectorDrawables {
            useSupportLibrary true
        }

        ksp {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += [
                '-Xjvm-default=all',
                // Allow experimental
                '-opt-in=kotlin.RequiresOptIn'
        ]
    }
    buildFeatures {
        viewBinding true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_compiler_version
    }
}

tasks.named("dokkaHtmlPartial") {
    dokkaSourceSets {
        configureEach {
            noAndroidSdkLink.set(false)
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.0-rc01'
    implementation 'com.google.android.material:material:1.8.0-alpha02'
    implementation 'androidx.preference:preference-ktx:1.2.0'

    // Testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:4.8.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation 'androidx.test.ext:junit-ktx:1.1.3'

    // KotlinX Coroutines
    def kotlinx_coroutines_version = '1.6.4'
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_coroutines_version"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines_version"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:$kotlinx_coroutines_version"

    // Android Jetpack
    def jetpack_work_version = '2.7.1'
    api "androidx.work:work-runtime-ktx:$jetpack_work_version"
    api "androidx.work:work-gcm:$jetpack_work_version"
    androidTestApi "androidx.work:work-testing:$jetpack_work_version"
    api "androidx.startup:startup-runtime:1.1.1"

    // Jetpack compose
    api "androidx.compose.ui:ui:$compose_version"
    api "androidx.compose.ui:ui-tooling:$compose_version"
    api "androidx.compose.ui:ui-util:$compose_version"
    api "androidx.compose.foundation:foundation:$compose_version"
    api "androidx.compose.material:material:$compose_version"
    api "androidx.compose.material3:material3:1.0.0-rc01" // Using Material Compose 3
    api "androidx.compose.material:material-icons-core:$compose_version"
    api "androidx.compose.material:material-icons-extended:$compose_version"
    api "androidx.compose.runtime:runtime-livedata:$compose_version"
    api "androidx.compose.runtime:runtime-rxjava2:$compose_version"
    api 'androidx.activity:activity-compose:1.6.0'
    api 'androidx.navigation:navigation-compose:2.5.2'
    androidTestApi "androidx.compose.ui:ui-test-junit4:$compose_version"

    // Jetpack Compose utilities - Remember preferences
    implementation 'dev.burnoo:compose-remember-preference:1.0.0'

    // Jetpack Compose utilities - Accompanist libraries
    def accompanist_version = '0.26.5-rc'
    api 'com.google.accompanist:accompanist-appcompat-theme:0.25.1'
    api "com.google.accompanist:accompanist-pager:$accompanist_version"
    api "com.google.accompanist:accompanist-drawablepainter:$accompanist_version"
    api "com.google.accompanist:accompanist-permissions:$accompanist_version"
    api "com.google.accompanist:accompanist-placeholder:$accompanist_version"

    // Jetpack Compose utilities - Icons
    api 'br.com.devsrsouza.compose.icons.android:font-awesome:1.0.0'
    api 'br.com.devsrsouza.compose.icons.android:erikflowers-weather-icons:1.0.0'

    // Local Libraries
    api project(":libs:app_intro")

    // Glide image loading
    def glide_version = '4.14.2'
    api "com.github.bumptech.glide:glide:$glide_version"
    ksp "com.github.bumptech.glide:compiler:$glide_version"

    // Jetpack Compose utilities - Glide
    def landscapist_glide_version = '2.0.0'
    api "com.github.skydoves:landscapist-glide:$landscapist_glide_version"

    // Jetpack Compose utilities - Charts
    implementation 'io.github.bytebeats:compose-charts:0.1.2'

    // Volley
    api 'com.android.volley:volley:1.2.1'

    // Lifecycle
    def lifecycle_version = '2.5.1'
    api "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    api "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    api "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    api "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    // Room DB engine
    def room_version = "2.4.3"
    implementation "androidx.room:room-runtime:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    // Google Firebase
    api platform('com.google.firebase:firebase-bom:31.0.1')
    api 'com.google.firebase:firebase-analytics-ktx'
    api 'com.google.firebase:firebase-crashlytics-ktx'
    api 'com.google.firebase:firebase-config-ktx'
    api 'com.google.firebase:firebase-perf-ktx'
    api 'com.google.firebase:firebase-dynamic-links-ktx'
    api 'com.google.firebase:firebase-messaging-ktx'
    api 'com.google.firebase:firebase-messaging-directboot:23.1.0'

    // Gson for JSON parsing
    implementation 'com.google.code.gson:gson:2.9.1'

    // Maps SDK
    api 'org.osmdroid:osmdroid-android:6.1.14'
    api 'com.github.MKergall:osmbonuspack:6.9.0'

    // AndroidX preferences
    api 'androidx.datastore:datastore-preferences:1.0.0'

    // Timber
    api 'com.jakewharton.timber:timber:5.0.1'
}
