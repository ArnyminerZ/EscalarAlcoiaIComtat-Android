plugins {
    id('com.android.application')
    id('kotlin-android')
    id('com.google.gms.google-services')
    id('com.google.firebase.crashlytics')
    id('com.google.firebase.firebase-perf')
    id('org.jetbrains.dokka')
    id('com.google.secrets_gradle_plugin') version '0.6'
    id('com.huawei.agconnect')
}

apply from: './version.gradle'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"
    ndkVersion "23.0.7123448"
    defaultConfig {
        applicationId "com.arnyminerz.escalaralcoiaicomtat"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode findProperty("android.versionCode") as Integer
        versionName VERSION_NAME
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs "en", "US", "ca", "es"
    }
    signingConfigs {
        release {
            def props = new Properties()
            file("${rootDir}/local.properties").withInputStream { props.load(it) }

            storeFile file("keystore.jks")
            storePassword props.getProperty("SIGNING_KEYSTORE_PASSWORD")
            keyAlias props.getProperty("SIGNING_KEY_ALIAS")
            keyPassword props.getProperty("SIGNING_KEY_PASSWORD")
        }
    }
    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            setProperty("archivesBaseName", "EscalarAlcoiaIComtat-${android.defaultConfig.versionCode}-${android.defaultConfig.versionName}")
            signingConfig signingConfigs.release

            kotlinOptions {
                freeCompilerArgs = ['-Xuse-experimental=kotlin.Experimental']
            }

            ndk.debugSymbolLevel = 'FULL'
        }
        debug {
            versionNameSuffix "-debug-build" + getDate()
            setProperty("archivesBaseName", "EscalarAlcoiaIComtat")

            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
        }
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dynamicFeatures = []

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }

    buildFeatures {
        viewBinding true
    }
}

tasks.named("dokkaHtml") {
    dokkaSourceSets {
        configureEach {
            noAndroidSdkLink.set(false)
        }
    }
}

android.applicationVariants.all { variant ->
    if (variant.buildType.name == "release") {
        variant.assemble.doLast {
            // This increases version code on release
            def versionPropsFile = file("../gradle.properties")
            Properties versionProps = new Properties()
            versionProps.load(new FileInputStream(versionPropsFile))
            def code = versionProps['android.versionCode'].toInteger() + 1
            versionProps['android.versionCode'] = code.toString()
            versionProps.store(versionPropsFile.newWriter(), null)
            logger.lifecycle("Increased version code to $code")
        }
    }
}

secrets {
    propertiesFileName 'local.properties'

    ignoreList.add("SIGNING_KEYSTORE_PASSWORD")
    ignoreList.add("SIGNING_KEY_ALIAS")
    ignoreList.add("SIGNING_KEY_PASSWORD")
}

configurations {
    all {
        exclude module: 'httpclient'
        exclude module: 'commons-logging'
    }
}

static def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmmss')
    return formattedDate
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.5.10"
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.core:core-ktx:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.android.material:material:1.3.0'

    implementation 'androidx.preference:preference-ktx:1.1.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // Google Play Core
    implementation 'com.google.android.play:core:1.10.0'
    implementation 'com.google.android.play:core-ktx:1.8.1'

    // Android Jetpack
    def jetpack_work_version = '2.7.0-alpha04'
    implementation "androidx.work:work-runtime-ktx:$jetpack_work_version"
    implementation "androidx.work:work-gcm:$jetpack_work_version"
    androidTestImplementation "androidx.work:work-testing:$jetpack_work_version"
    implementation "androidx.startup:startup-runtime:1.0.0"

    // Google Firebase
    implementation platform('com.google.firebase:firebase-bom:28.1.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-firestore-ktx'
    implementation 'com.google.firebase:firebase-storage-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-config-ktx'
    implementation 'com.google.firebase:firebase-perf-ktx'
    implementation 'com.google.firebase:firebase-dynamic-links-ktx'
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.firebase:firebase-messaging-ktx'
    implementation 'com.google.firebase:firebase-messaging-directboot:22.0.0'

    // Firebase Auth Google Sign-In
    implementation 'com.google.android.gms:play-services-auth:19.0.0'

    // Maps SDK
    implementation 'com.github.ArnyminerZ:MapKit:1.0.2-dev6'
    implementation 'com.google.android.gms:play-services-maps:17.0.1'

    // KotlinX Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.1.1'

    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'

    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    implementation 'io.noties.markwon:core:4.6.2'
}
